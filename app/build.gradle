apply plugin: 'com.android.application'
apply from: 'sdktools.gradle'

Properties props = new Properties()
File gradlePropertiesFile = file("gradle.properties")
if (!gradlePropertiesFile.exists()) {
    throw new IllegalStateException(
            "Please create the file 'gradle.properties' before " +
                    "importing the project. Do not forget to add custom values!"
    )
}
props.load(new FileInputStream(gradlePropertiesFile))



android {
    // compileSdkVersion 21
    compileSdkVersion project.getSDKIfPossible(21)
    // buildToolsVersion "21.1.1"
    buildToolsVersion project.getHighestAvailableTools("21.1.1")

    defaultConfig {
        versionCode 34
        versionName "1.30.4"
        minSdkVersion 14
        targetSdkVersion 21
        project.ext.set("archivesBaseName", "Fahrplan");
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def file = output.outputFile
            if (file != null && file.name.endsWith('.apk')) {
                def fileName = file.name.replace(".apk", "-" + versionName + ".apk")
                output.outputFile = new File(file.parent, fileName)
            }
        }
    }

    buildTypes {
        debug {
            versionNameSuffix "-DEBUG"
            applicationIdSuffix ".debug"
            zipAlignEnabled true
            debuggable true
        }
        release {
            zipAlignEnabled true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
        }
    }

    signingConfigs {
        camp2015 {
            storeFile file(props['signing.camp2015-release.keystoreFilePath'])
            storePassword props['signing.camp2015-release.keystorePassword']
            keyAlias props['signing.camp2015-release.keyAlias']
            keyPassword props['signing.camp2015-release.keyPassword']
        }
    }

    productFlavors {
        camp2015 {
            applicationId "nerd.tuxmobil.fahrplan.congress"
            signingConfig signingConfigs.camp2015
            buildConfigField "String", "SCHEDULE_DOMAIN", '"events.ccc.de"'
            buildConfigField "String", "SCHEDULE_DOMAIN_PART", '"http://events.ccc.de"'
            buildConfigField "String", "SCHEDULE_EVENT_PART", '"/events/%1$s.html"'
            buildConfigField "String", "SCHEDULE_PART", '"/camp/2015/Fahrplan"'
            buildConfigField "String", "SCHEDULE_PATH", '"/camp/2015/Fahrplan/schedule.xml"'
            buildConfigField "int", "SCHEDULE_FIRST_DAY_START_YEAR", "2015"
            buildConfigField "int", "SCHEDULE_FIRST_DAY_START_MONTH", "8"
            buildConfigField "int", "SCHEDULE_FIRST_DAY_START_DAY", "13"
            buildConfigField "int", "SCHEDULE_LAST_DAY_END_YEAR", "2015"
            buildConfigField "int", "SCHEDULE_LAST_DAY_END_MONTH", "8"
            buildConfigField "int", "SCHEDULE_LAST_DAY_END_DAY", "17"
            buildConfigField "boolean", "SCHEDULE_SUPPORTS_HTTPS", "true"
            buildConfigField "String", "SCHEDULE_FEEDBACK_URL", '"https://frab.cccv.de/en/camp2015/public/events/%s/feedback/new"'
        }
    }
}

dependencies {
    compile "com.android.support:appcompat-v7:21.0.+"
    compile 'com.afollestad:material-dialogs:0.4.6'
}

repositories {
    maven { url 'http://clinker.47deg.com/nexus/content/groups/public' }
}
