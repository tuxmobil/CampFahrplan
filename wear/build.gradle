apply plugin: 'com.android.application'

Properties props = new Properties()
File gradlePropertiesFile = file("gradle.properties")
if (!gradlePropertiesFile.exists()) {
    throw new IllegalStateException(
            "Please create the file 'gradle.properties' before " +
                    "importing the project. Do not forget to add custom values!"
    )
}
props.load(new FileInputStream(gradlePropertiesFile))



android {
    compileSdkVersion 21
    buildToolsVersion "21.1.1"

    defaultConfig {
        applicationId "nerd.tuxmobil.fahrplan.congress"
        minSdkVersion 20
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def file = output.outputFile
            if (file != null && file.name.endsWith('.apk')) {
                def fileName = file.name.replace(".apk", "-" + versionName + ".apk")
                output.outputFile = new File(file.parent, fileName)
            }
        }
    }

    buildTypes {
        debug {
            versionNameSuffix "-DEBUG"
            applicationIdSuffix ".debug"
            zipAlignEnabled true
        }
        release {
            zipAlignEnabled true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
        }
    }

    signingConfigs {
        ccc31c3 {
            storeFile file(props['signing.ccc31c3-release.keystoreFilePath'])
            storePassword props['signing.ccc31c3-release.keystorePassword']
            keyAlias props['signing.ccc31c3-release.keyAlias']
            keyPassword props['signing.ccc31c3-release.keyPassword']
        }
    }

    productFlavors {
        ccc31c3 {
            applicationId 'nerd.tuxmobil.fahrplan.congress'
            signingConfig signingConfigs.ccc31c3
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.google.android.support:wearable:1.0.0'
    compile 'com.google.android.gms:play-services-wearable:6.1.71'
    compile 'com.google.android.support:wearable:1.1.0'
}
